pkgname=sleex-microtex-git
_pkgname=MicroTeX
pkgver=r494.0e3707f
pkgrel=3
pkgdesc='MicroTeX for Sleex.'
#pkgdesc="A dynamic, cross-platform, and embeddable LaTeX rendering library"
arch=("x86_64")
url="https://github.com/NanoMichael/${_pkgname}"
license=('MIT')
depends=('tinyxml2' 'gtkmm3' 'gtksourceviewmm' 'cairomm')
makedepends=("git" "cmake")
source=("git+${url}.git")
sha256sums=("SKIP")
provides=('illogical-impulse-microtex-git')
replaces=('illogical-impulse-microtex-git')
conflicts=('illogical-impulse-microtex-git')


pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd $_pkgname
  cmake -B build -S . -DCMAKE_BUILD_TYPE=None
  cmake --build build
}

package() {
  cd $_pkgname
  install -Dm0755 -t "$pkgdir/opt/$_pkgname/" build/LaTeX
  cp -r build/res "$pkgdir/opt/$_pkgname/"
  install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
